# Toolchain
include_guard(GLOBAL)
set(VCPKG_TARGET_TRIPLET "linux" CACHE STRING "")
cmake_path(GET CMAKE_CURRENT_LIST_DIR PARENT_PATH ACE)

# System
set(CMAKE_CROSSCOMPILING OFF CACHE INTERNAL "" FORCE)
set(CMAKE_SYSTEM_NAME "Linux" CACHE INTERNAL "" FORCE)
set(CMAKE_SYSTEM_VERSION "5.10.0" CACHE INTERNAL "" FORCE)
set(CMAKE_SYSTEM_PROCESSOR "x86_64" CACHE INTERNAL "" FORCE)

set(CMAKE_COMPILER_TARGET "x86_64-pc-linux-gnu" CACHE INTERNAL "")
set(CMAKE_C_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_CXX_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_NASM_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_SYSROOT "${ACE}/sys/linux" CACHE INTERNAL "" FORCE)

# Runtime Path
set(CMAKE_BUILD_RPATH_USE_ORIGIN ON CACHE BOOL "")

# Search Paths
list(APPEND CMAKE_MODULE_PATH "${ACE}/res/cmake")
set(CMAKE_SYSTEM_PROGRAM_PATH "${ACE}/build/host/bin" CACHE PATH "")

set(CMAKE_FIND_ROOT_PATH "${CMAKE_SYSROOT};${ACE}/build/host/linux" CACHE INTERNAL "")
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM BOTH CACHE INTERNAL "" FORCE)

set(ENV{PKG_CONFIG_SYSROOT_DIR} "${CMAKE_SYSROOT}")
set(ENV{PKG_CONFIG_PATH} "${CMAKE_SYSROOT}/usr/lib/x86_64-linux-gnu/pkgconfig")
include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/lib/x86_64-linux-gnu/glib-2.0/include")
include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/include/glib-2.0")
include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/lib/x86_64-linux-gnu/dbus-1.0/include")
include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/include/dbus-1.0")
include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/include/ibus-1.0")

# Compiler
set(CMAKE_C_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_CXX_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_NASM_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)

set(CMAKE_C_COMPILER "${ACE}/build/host/bin/clang" CACHE FILEPATH "")
set(CMAKE_CXX_COMPILER "${ACE}/build/host/bin/clang++" CACHE FILEPATH "")
set(CMAKE_CXX_COMPILER_CLANG_SCAN_DEPS "${ACE}/build/host/bin/clang-scan-deps" CACHE FILEPATH "")
set(CMAKE_ASM_COMPILER "${ACE}/build/host/bin/clang" CACHE FILEPATH "")
set(CMAKE_ASM_NASM_COMPILER "${ACE}/build/host/bin/yasm" CACHE FILEPATH "")
set(CMAKE_LINKER "${ACE}/build/host/bin/ld.lld" CACHE FILEPATH "")

set(CMAKE_AR "${ACE}/build/host/bin/llvm-ar" CACHE FILEPATH "")
set(CMAKE_NM "${ACE}/build/host/bin/llvm-nm" CACHE FILEPATH "")
set(CMAKE_RANLIB "${ACE}/build/host/bin/llvm-ranlib" CACHE FILEPATH "")
set(CMAKE_OBJCOPY "${ACE}/build/host/bin/llvm-objcopy" CACHE FILEPATH "")
set(CMAKE_OBJDUMP "${ACE}/build/host/bin/llvm-objdump" CACHE FILEPATH "")
set(CMAKE_STRIP "${ACE}/build/host/bin/llvm-strip" CACHE FILEPATH "")
set(CMAKE_SIZE "${ACE}/build/host/bin/llvm-size" CACHE FILEPATH "")

# Compiler Flags
set(CMAKE_C_EXTENSIONS OFF CACHE BOOL "")
set(CMAKE_CXX_EXTENSIONS OFF CACHE BOOL "")

set(CMAKE_C_FLAGS_INIT "-march=x86-64-v3 -mavx2 -fPIC")
set(CMAKE_C_FLAGS_RELEASE_INIT "-O3 -DNDEBUG -fomit-frame-pointer -flto")

set(CMAKE_CXX_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} -fstrict-vtable-pointers -fno-exceptions")
set(CMAKE_CXX_FLAGS_RELEASE_INIT "${CMAKE_C_FLAGS_RELEASE_INIT} -fwhole-program-vtables")

if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(CMAKE_C_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} ${VCPKG_C_FLAGS}")
  set(CMAKE_C_FLAGS_RELEASE_INIT "${CMAKE_C_FLAGS_RELEASE_INIT} ${VCPKG_C_FLAGS_RELEASE}")

  set(CMAKE_CXX_FLAGS_INIT "${CMAKE_CXX_FLAGS_INIT} -fno-rtti ${VCPKG_CXX_FLAGS}")
  set(CMAKE_CXX_FLAGS_RELEASE_INIT "${CMAKE_CXX_FLAGS_RELEASE_INIT} ${VCPKG_CXX_FLAGS_RELEASE}")
endif()

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE_INIT}" CACHE STRING "")

set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE_INIT}" CACHE STRING "")

# Modules
set(CMAKE_CXX_SCAN_FOR_MODULES OFF CACHE BOOL "")

# Optimizations
cmake_policy(SET CMP0069 NEW)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELEASE OFF CACHE BOOL "")

# Code Generation
cmake_policy(SET CMP0083 NEW)
set(CMAKE_POSITION_INDEPENDENT_CODE OFF CACHE BOOL "")

# Linker Flags
foreach(LINKER SHARED MODULE EXE)
  set(CMAKE_${LINKER}_LINKER_FLAGS_INIT "-pthread")
  set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT "-s")
  if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
    set(CMAKE_${LINKER}_LINKER_FLAGS_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_INIT} ${VCPKG_LINKER_FLAGS}")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT} ${VCPKG_LINKER_FLAGS_RELEASE}")
  endif()
endforeach()

# Platform Variables
set(CMAKE_TRY_COMPILE_PLATFORM_VARIABLES
  CMAKE_BUILD_RPATH
  CMAKE_TOOLCHAIN_FILE
  CACHE STRING "")

# Vcpkg
if(NOT DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(ENV{VCPKG_DISABLE_METRICS} "1")
  set(ENV{VCPKG_FORCE_SYSTEM_BINARIES} "1")
  set(ENV{VCPKG_WORKS_SYSTEM_BINARIES} "1")
  set(VCPKG_ROOT "${ACE}/vcpkg" CACHE PATH "")
  set(VCPKG_INSTALL_OPTIONS "--x-no-default-features")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--no-print-usage")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--overlay-ports=${ACE}/res/ports")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--overlay-triplets=${ACE}/src/triplets")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--x-buildtrees-root=${ACE}/build/host/buildtrees")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--x-packages-root=${ACE}/build/host/packages")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--x-install-root=${ACE}/build/host")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--feature-flags=-binarycaching")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--host-triplet=linux")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--triplet=linux")
  set(VCPKG_KEEP_ENV_VARS "PKG_CONFIG_SYSROOT_DIR;PKG_CONFIG_PATH")
  set(Z_VCPKG_POWERSHELL_PATH "${ACE}/share/powershell/pwsh")
  include("${VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake")
endif()
