# Toolchain
include_guard(GLOBAL)
cmake_path(GET CMAKE_CURRENT_LIST_DIR PARENT_PATH ACE)

# System
set(CMAKE_CROSSCOMPILING OFF CACHE INTERNAL "" FORCE)
set(CMAKE_SYSTEM_NAME "Windows" CACHE INTERNAL "" FORCE)
set(CMAKE_SYSTEM_VERSION "10.0" CACHE INTERNAL "" FORCE)
set(CMAKE_SYSTEM_PROCESSOR "AMD64" CACHE INTERNAL "" FORCE)

set(CMAKE_COMPILER_TARGET "x86_64-w64-windows-gnu" CACHE INTERNAL "")
set(CMAKE_C_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_CXX_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_NASM_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_SYSROOT "${ACE}" CACHE INTERNAL "" FORCE)

# Search Paths
list(APPEND CMAKE_MODULE_PATH "${ACE}/res/cmake")
set(CMAKE_SYSTEM_PROGRAM_PATH "${ACE}/bin" CACHE PATH "")

set(CMAKE_FIND_ROOT_PATH "${ACE};${ACE}/vcpkg/installed/mingw" CACHE INTERNAL "")
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM BOTH CACHE INTERNAL "" FORCE)

# Generator
if(CMAKE_GENERATOR MATCHES "[Nn]inja")
  set(CMAKE_MAKE_PROGRAM "${ACE}/bin/ninja.exe" CACHE FILEPATH "")
endif()

# Compiler
set(CMAKE_C_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_CXX_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_NASM_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)

set(CMAKE_C_COMPILER "${ACE}/bin/clang.exe" CACHE FILEPATH "")
set(CMAKE_CXX_COMPILER "${ACE}/bin/clang++.exe" CACHE FILEPATH "")
set(CMAKE_CXX_COMPILER_CLANG_SCAN_DEPS "${ACE}/bin/clang-scan-deps.exe" CACHE FILEPATH "")
set(CMAKE_ASM_COMPILER "${ACE}/bin/clang.exe" CACHE FILEPATH "")
set(CMAKE_ASM_NASM_COMPILER "${ACE}/bin/yasm.exe" CACHE FILEPATH "")
set(CMAKE_LINKER "${ACE}/bin/lld-link.exe" CACHE FILEPATH "")

set(CMAKE_AR "${ACE}/bin/llvm-ar.exe" CACHE FILEPATH "")
set(CMAKE_NM "${ACE}/bin/llvm-nm.exe" CACHE FILEPATH "")
set(CMAKE_MT "${ACE}/bin/llvm-mt.exe" CACHE FILEPATH "")
set(CMAKE_RANLIB "${ACE}/bin/llvm-ranlib.exe" CACHE FILEPATH "")
set(CMAKE_DLLTOOL "${ACE}/bin/llvm-dlltool.exe" CACHE FILEPATH "")
set(CMAKE_OBJCOPY "${ACE}/bin/llvm-objcopy.exe" CACHE FILEPATH "")
set(CMAKE_OBJDUMP "${ACE}/bin/llvm-objdump.exe" CACHE FILEPATH "")
set(CMAKE_STRIP "${ACE}/bin/llvm-strip.exe" CACHE FILEPATH "")
set(CMAKE_SIZE "${ACE}/bin/llvm-size.exe" CACHE FILEPATH "")

set(CMAKE_RC_COMPILER "${ACE}/bin/llvm-windres.exe" CACHE FILEPATH "")
set(CMAKE_RC_FLAGS_INIT "-I ${ACE}/include")

# Compiler Flags
set(CMAKE_C_EXTENSIONS OFF CACHE BOOL "")
set(CMAKE_CXX_EXTENSIONS OFF CACHE BOOL "")

# Visual Studio 2022 Version 17.14 sets _MSC_VER to 1944.
# https://learn.microsoft.com/cpp/overview/compiler-versions?view=msvc-170
# MinGW is already configured to define WINVER=0x0A00 and _WIN32_WINNT=0x0A00.
set(CMAKE_C_FLAGS_INIT "-march=x86-64-v2 -fms-compatibility-version=19.44")
set(CMAKE_C_FLAGS_DEBUG_INIT "-Og -g")
set(CMAKE_C_FLAGS_RELEASE_INIT "-O3 -DNDEBUG -fomit-frame-pointer -flto")
set(CMAKE_C_FLAGS_MINSIZEREL_INIT "-Oz -DNDEBUG -fomit-frame-pointer")
set(CMAKE_C_FLAGS_RELWITHDEBINFO_INIT "-O2 -g -DNDEBUG")
set(CMAKE_C_FLAGS_COVERAGE_INIT "-O0 -g -fno-omit-frame-pointer -fprofile-instr-generate -fcoverage-mapping")
set(CMAKE_C_FLAGS_PROFILE_INIT "-O3 -g -fno-omit-frame-pointer -fprofile-instr-generate")

if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(CMAKE_C_FLAGS_DEBUG_INIT "-O3 -DNDEBUG")
endif()

set(CMAKE_CXX_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} -fstrict-vtable-pointers -fno-exceptions -fno-rtti")
set(CMAKE_CXX_FLAGS_DEBUG_INIT "${CMAKE_C_FLAGS_DEBUG_INIT}")
set(CMAKE_CXX_FLAGS_RELEASE_INIT "${CMAKE_C_FLAGS_RELEASE_INIT} -fwhole-program-vtables")
set(CMAKE_CXX_FLAGS_MINSIZEREL_INIT "${CMAKE_C_FLAGS_MINSIZEREL_INIT}")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO_INIT "${CMAKE_C_FLAGS_RELWITHDEBINFO_INIT}")
set(CMAKE_CXX_FLAGS_COVERAGE_INIT "${CMAKE_C_FLAGS_COVERAGE_INIT}")
set(CMAKE_CXX_FLAGS_PROFILE_INIT "${CMAKE_C_FLAGS_PROFILE_INIT}")

if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(CMAKE_C_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} ${VCPKG_C_FLAGS}")
  set(CMAKE_C_FLAGS_DEBUG_INIT "${CMAKE_C_FLAGS_DEBUG_INIT} ${VCPKG_C_FLAGS_DEBUG}")
  set(CMAKE_C_FLAGS_RELEASE_INIT "${CMAKE_C_FLAGS_RELEASE_INIT} ${VCPKG_C_FLAGS_RELEASE}")

  set(CMAKE_CXX_FLAGS_INIT "${CMAKE_CXX_FLAGS_INIT} ${VCPKG_CXX_FLAGS}")
  set(CMAKE_CXX_FLAGS_DEBUG_INIT "${CMAKE_CXX_FLAGS_DEBUG_INIT} ${VCPKG_CXX_FLAGS_DEBUG}")
  set(CMAKE_CXX_FLAGS_RELEASE_INIT "${CMAKE_CXX_FLAGS_RELEASE_INIT} ${VCPKG_CXX_FLAGS_RELEASE}")
endif()

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_MINSIZEREL "${CMAKE_C_FLAGS_MINSIZEREL_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_RELWITHDEBINFO "${CMAKE_C_FLAGS_RELWITHDEBINFO_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_COVERAGE "${CMAKE_C_FLAGS_COVERAGE_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_PROFILE "${CMAKE_C_FLAGS_PROFILE_INIT}" CACHE STRING "")

set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_MINSIZEREL "${CMAKE_CXX_FLAGS_MINSIZEREL_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_COVERAGE "${CMAKE_CXX_FLAGS_COVERAGE_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_PROFILE "${CMAKE_CXX_FLAGS_PROFILE_INIT}" CACHE STRING "")

# Modules
set(CMAKE_CXX_SCAN_FOR_MODULES OFF CACHE BOOL "")

# Optimizations
cmake_policy(SET CMP0069 NEW)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_DEBUG OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELEASE OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_MINSIZEREL OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELWITHDEBINFO OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_COVERAGE OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_PROFILE OFF CACHE BOOL "")

# Runtime Library
cmake_policy(SET CMP0091 NEW)
set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:DLL>" CACHE INTERNAL "" FORCE)

# Debug Information
cmake_policy(SET CMP0141 NEW)
set(CMAKE_MSVC_DEBUG_INFORMATION_FORMAT "" CACHE INTERNAL "" FORCE)

# Linker Flags
cmake_policy(SET CMP0056 NEW)
foreach(LINKER SHARED MODULE EXE)
  set(CMAKE_${LINKER}_LINKER_FLAGS_INIT "-Xlinker /MANIFEST:NO")
  if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
    set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT "-s -Xlinker /OPT:REF -Xlinker /OPT:ICF -Xlinker /INCREMENTAL")
  else()
    set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL")
  endif()
  set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT "-s -Xlinker /OPT:REF -Xlinker /OPT:ICF -Xlinker /INCREMENTAL:NO")
  set(CMAKE_${LINKER}_LINKER_FLAGS_MINSIZEREL_INIT "-s -Xlinker /OPT:REF -Xlinker /OPT:ICF -Xlinker /INCREMENTAL:NO")
  set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL")
  set(CMAKE_${LINKER}_LINKER_FLAGS_COVERAGE_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL")
  set(CMAKE_${LINKER}_LINKER_FLAGS_PROFILE_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL:NO")

  if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
    set(CMAKE_${LINKER}_LINKER_FLAGS_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_INIT} ${VCPKG_LINKER_FLAGS}")
    set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT} ${VCPKG_LINKER_FLAGS_DEBUG}")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT} ${VCPKG_LINKER_FLAGS_RELEASE}")
  endif()

  set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT
   "${CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT} -L${ACE}/lib/shared")
  set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT
   "${CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT} -L${ACE}/lib/shared")
endforeach()

# Configurations
set_property(GLOBAL PROPERTY DEBUG_CONFIGURATIONS Debug RelWithDebInfo Coverage Profile)
set(CMAKE_MAP_IMPORTED_CONFIG_MINSIZEREL ";Release" CACHE INTERNAL "" FORCE)
set(CMAKE_MAP_IMPORTED_CONFIG_RELWITHDEBINFO ";Debug" CACHE INTERNAL "" FORCE)
set(CMAKE_MAP_IMPORTED_CONFIG_COVERAGE ";Debug" CACHE INTERNAL "" FORCE)
set(CMAKE_MAP_IMPORTED_CONFIG_PROFILE ";Debug" CACHE INTERNAL "" FORCE)

# Platform Variables
set(CMAKE_TRY_COMPILE_PLATFORM_VARIABLES
  BUILD_SHARED_LIBS
  CMAKE_BUILD_RPATH
  CMAKE_TOOLCHAIN_FILE
  CACHE STRING "")

# Tests
set_property(GLOBAL PROPERTY CTEST_TARGETS_ADDED 1)

# Vcpkg
if(NOT DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(ENV{VCPKG_DISABLE_METRICS} "1")
  set(ENV{VCPKG_FORCE_SYSTEM_BINARIES} "1")
  set(ENV{VCPKG_WORKS_SYSTEM_BINARIES} "1")
  set(VCPKG_ROOT "${ACE}/vcpkg" CACHE PATH "")
  set(VCPKG_INSTALL_OPTIONS "--x-no-default-features")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--no-print-usage")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--overlay-ports=${ACE}/res/ports")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--overlay-triplets=${ACE}/res/triplets")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--feature-flags=-binarycaching")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--host-triplet=mingw")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--triplet=mingw")
  include("${VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake")
endif()
