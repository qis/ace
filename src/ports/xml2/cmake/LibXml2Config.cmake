# LibXml2
# https://cmake.org/cmake/help/latest/module/FindLibXml2.html
#
#   find_package(LibXml2 REQUIRED)
#   target_link_libraries(main PRIVATE LibXml2::LibXml2)
#
cmake_policy(PUSH)
cmake_policy(VERSION 3.20)
set(CMAKE_IMPORT_FILE_VERSION 1)

enable_language(CXX)

set(LIBXML2_VERSION_STRING ${LibXml2_VERSION})
set(LIBXML2_VERSION ${LIBXML2_VERSION_STRING})
set(LIBXML2_INCLUDE_DIRS)

set(LIBXML2_LIBRARIES LibXml2::LibXml2)

include(CMakeFindDependencyMacro)
find_dependency(ICU REQUIRED COMPONENTS data uc)
find_dependency(LibLZMA REQUIRED)
find_dependency(ZLIB REQUIRED)

include(AceImportLibrary)
ace_import_library(LibXml2::LibXml2 C
  NAMES xml2 libxml2 HEADERS_LOCATIONS libxml2 HEADERS libxml/parser.h
  LINK_LIBRARIES ICU::data ICU::uc LibLZMA::LibLZMA ZLIB::ZLIB
  COMPILE_DEFINITIONS_STATIC LIBXML_STATIC)

set(LIBXML2_XMLCATALOG_EXECUTABLE LIBXML2_XMLCATALOG_EXECUTABLE-NOTFOUND CACHE INTERNAL "")
set(LIBXML2_XMLLINT_EXECUTABLE LIBXML2_XMLLINT_EXECUTABLE-NOTFOUND CACHE INTERNAL "")

string(REPLACE "." ";" LIBXML2_VERSION_LIST ${LIBXML2_VERSION})
list(GET LIBXML2_VERSION_LIST 0 LIBXML2_VERSION_MAJOR)
list(GET LIBXML2_VERSION_LIST 1 LIBXML2_VERSION_MINOR)
list(GET LIBXML2_VERSION_LIST 2 LIBXML2_VERSION_PATCH)
set(LIBXML2_VERSION_TWEAK 0)

set(LIBXML2_MODULES ON)

set(LIBXML2_INCLUDE_DIR "${LIBXML2_INCLUDE_DIRS}" CACHE STRING "")
set(LIBXML2_LIBRARY "${LIBXML2_LIBRARIES}" CACHE STRING "")
set(LIBXML2_FOUND 1)

set(CMAKE_IMPORT_FILE_VERSION)
cmake_policy(POP)
