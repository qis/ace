vcpkg_download_distfile(ARCHIVE
  FILENAME "boost-${VERSION}.tar.xz"
  URLS "https://github.com/boostorg/boost/releases/download/boost-${VERSION}/boost-${VERSION}.tar.xz"
  SHA512 6f3be2b3a5de7b5030afcc1e02bc6543f7b863f3d2137a06d065ba28febfbda1ae07dd7918e1c7c4b36b7f0bfabba64197c3e552c4960bb2f2fd9efb6fcd4216)

vcpkg_extract_source_archive(SOURCE_PATH
  ARCHIVE "${ARCHIVE}")

set(BOOST_LOCALE_ENABLE_POSIX OFF)
if(VCPKG_TARGET_IS_LINUX)
  set(BOOST_LOCALE_ENABLE_POSIX ON)
endif()

set(BOOST_LOCALE_ENABLE_WINAPI OFF)
if(VCPKG_TARGET_IS_MINGW)
  set(BOOST_LOCALE_ENABLE_WINAPI ON)
endif()

set(BOOST_STANDARD_LIBRARIES "")
if(VCPKG_TARGET_IS_MINGW)
  set(BOOST_STANDARD_LIBRARIES "-lws2_32")
endif()

set(BOOST_BUILD_SHARED_LIBS OFF)
if(VCPKG_LIBRARY_LINKAGE STREQUAL "dynamic")
  set(BOOST_BUILD_SHARED_LIBS ON)
endif()

vcpkg_cmake_configure(
  SOURCE_PATH "${SOURCE_PATH}"
  MAYBE_UNUSED_VARIABLES
    CMAKE_CXX_STANDARD_LIBRARIES
  OPTIONS
    -DBOOST_ENABLE_MPI=OFF
    -DBOOST_ENABLE_PYTHON=OFF
    -DBOOST_INSTALL_LAYOUT=system
    -DBOOST_IOSTREAMS_ENABLE_ZLIB=ON
    -DBOOST_IOSTREAMS_ENABLE_BZIP2=ON
    -DBOOST_IOSTREAMS_ENABLE_LZMA=ON
    -DBOOST_IOSTREAMS_ENABLE_ZSTD=ON
    -DBOOST_LOCALE_ENABLE_ICONV=OFF
    -DBOOST_LOCALE_ENABLE_ICU=ON
    -DBOOST_LOCALE_ENABLE_STD=OFF
    -DBOOST_LOCALE_ENABLE_POSIX=${BOOST_LOCALE_ENABLE_POSIX}
    -DBOOST_LOCALE_ENABLE_WINAPI=${BOOST_LOCALE_ENABLE_WINAPI}
    -DBOOST_STACKTRACE_ENABLE_NOOP=ON
    -DBOOST_STACKTRACE_ENABLE_BACKTRACE=ON
    -DBOOST_STACKTRACE_ENABLE_ADDR2LINE=OFF
    -DBOOST_STACKTRACE_ENABLE_BASIC=ON
    -DBOOST_STACKTRACE_ENABLE_WINDBG=OFF
    -DBOOST_STACKTRACE_ENABLE_WINDBG_CACHED=OFF
    -DBOOST_INSTALL_CMAKEDIR=share
    -DCMAKE_CXX_STANDARD_LIBRARIES=${BOOST_STANDARD_LIBRARIES}
    -DBUILD_SHARED_LIBS=${BOOST_BUILD_SHARED_LIBS})

vcpkg_cmake_install()

file(REMOVE_RECURSE "${CURRENT_PACKAGES_DIR}/debug/include")
file(INSTALL "${CMAKE_CURRENT_LIST_DIR}/usage" DESTINATION "${CURRENT_PACKAGES_DIR}/share/${PORT}")

vcpkg_install_copyright(FILE_LIST "${SOURCE_PATH}/LICENSE_1_0.txt")
vcpkg_cmake_config_fixup()
vcpkg_copy_pdbs()
