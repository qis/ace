# System
include(${ACE}/sys/arch.cmake)

# Search Paths
set(CMAKE_SYSTEM_INCLUDE_PATH ${ACE_TARGET_ROOT}/include CACHE PATH "")

# Compiler
set(CMAKE_C_COMPILER_WORKS ON CACHE BOOL "" FORCE)
set(CMAKE_CXX_COMPILER_WORKS ON CACHE BOOL "" FORCE)
set(CMAKE_ASM_COMPILER_WORKS ON CACHE BOOL "" FORCE)

set(CMAKE_C_COMPILER cl.exe CACHE STRING "" FORCE)
set(CMAKE_CXX_COMPILER cl.exe CACHE STRING "" FORCE)
set(CMAKE_ASM_COMPILER cl.exe CACHE STRING "" FORCE)

set(CMAKE_ASM_MASM_COMPILER ml64.exe CACHE STRING "" FORCE)
set(CMAKE_ASM_NASM_COMPILER ${ACE}/bin/yasm.exe CACHE PATH "")
set(CMAKE_ASM_MASM_FLAGS_INIT "/nologo")

# Compiler Flags
set(CMAKE_C_EXTENSIONS OFF CACHE BOOL "")
set(CMAKE_C_STANDARD_DEFAULT 11 CACHE INTERNAL "")

set(CMAKE_CXX_EXTENSIONS OFF CACHE BOOL "")
set(CMAKE_CXX_STANDARD_DEFAULT 20 CACHE INTERNAL "")

cmake_policy(SET CMP0091 NEW)
cmake_policy(SET CMP0092 NEW)

if(NOT BUILD_SHARED_LIBS AND (NOT "${VCPKG_CRT_LINKAGE}" STREQUAL "dynamic"))
  set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<NOT:$<CONFIG:Release>>:DLL>" CACHE STRING "")
else()
  set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreadedDLL" CACHE STRING "")
endif()

set(CMAKE_INTERPROCEDURAL_OPTIMIZATION ON CACHE STRING "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_DEBUG OFF CACHE INTERNAL "" FORCE)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELEASE OFF CACHE INTERNAL "" FORCE)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_MINSIZEREL OFF CACHE INTERNAL "" FORCE)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELWITHDEBINFO OFF CACHE INTERNAL "" FORCE)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_COVERAGE OFF CACHE INTERNAL "" FORCE)

set(CFLAGS_GL)
set(LDFLAGS_LTCG)
if(CMAKE_INTERPROCEDURAL_OPTIMIZATION)
  set(CFLAGS_GL "/GL")
  set(LDFLAGS_LTCG "/LTCG")
endif()

set(CFLAGS "/permissive- /FC /DWINVER=0x0A00 /D_WIN32_WINNT=0x0A00")

set(CMAKE_C_FLAGS_INIT "${CFLAGS}")
set(CMAKE_C_FLAGS_DEBUG "/RTC1 /Zi" CACHE STRING "")
set(CMAKE_C_FLAGS_RELEASE "/O2 /DNDEBUG ${CFLAGS_GL}" CACHE STRING "")
set(CMAKE_C_FLAGS_MINSIZEREL "/O1 /Ob1 /DNDEBUG" CACHE STRING "")
set(CMAKE_C_FLAGS_RELWITHDEBINFO "/O1 /Ob1 /Zi /DNDEBUG" CACHE STRING "")

set(CMAKE_CXX_FLAGS_INIT "${CFLAGS}")
set(CMAKE_CXX_FLAGS_DEBUG "/RTC1 /Zi" CACHE STRING "")
set(CMAKE_CXX_FLAGS_RELEASE "/O2 /DNDEBUG ${CFLAGS_GL}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_MINSIZEREL "/O1 /Ob1 /DNDEBUG" CACHE STRING "")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "/O1 /Ob1 /Zi /DNDEBUG" CACHE STRING "")

unset(CFLAGS)
unset(CFLAGS_GL)

# Include Directories
set(CMAKE_C_STANDARD_INCLUDE_DIRECTORIES ${CMAKE_SYSTEM_INCLUDE_PATH} CACHE STRING "")
set(CMAKE_CXX_STANDARD_INCLUDE_DIRECTORIES ${CMAKE_SYSTEM_INCLUDE_PATH} CACHE STRING "")
set(CMAKE_RC_STANDARD_INCLUDE_DIRECTORIES ${CMAKE_SYSTEM_INCLUDE_PATH} CACHE STRING "")

# Linker Flags
foreach(LINKER SHARED MODULE EXE)
  set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG "/DEBUG /INCREMENTAL" CACHE STRING "")
  set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE "/OPT:REF /OPT:ICF /INCREMENTAL:NO ${LDFLAGS_LTCG}" CACHE STRING "")
  set(CMAKE_${LINKER}_LINKER_FLAGS_MINSIZEREL "/OPT:REF /OPT:ICF /INCREMENTAL:NO" CACHE STRING "")
  set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO "/DEBUG /INCREMENTAL" CACHE STRING "")
endforeach()

unset(LDFLAGS_LTCG)

# Resource Compiler
set(CMAKE_RC_COMPILER rc.exe CACHE STRING "" FORCE)

# Resource Compiler Flags
set(CMAKE_RC_FLAGS "/nologo -DWIN32" CACHE STRING "")

# Tools
set(CMAKE_MT mt.exe CACHE STRING "" FORCE)

# Debug Configurations
set_property(GLOBAL PROPERTY DEBUG_CONFIGURATIONS Debug RelWithDebInfo)
