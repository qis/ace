# Toolchain
include_guard(GLOBAL)
cmake_path(GET CMAKE_CURRENT_LIST_DIR PARENT_PATH ACE)

# System
if(VCPKG_TARGET_TRIPLET STREQUAL "linux")
  set(CMAKE_SYSTEM_NAME "Linux" CACHE INTERNAL "" FORCE)
  set(CMAKE_SYSTEM_VERSION "5.10.0" CACHE INTERNAL "" FORCE)
  set(CMAKE_SYSTEM_PROCESSOR "x86_64" CACHE INTERNAL "" FORCE)
  set(CMAKE_COMPILER_TARGET "x86_64-pc-linux-gnu" CACHE INTERNAL "")
elseif(VCPKG_TARGET_TRIPLET STREQUAL "mingw")
  set(CMAKE_SYSTEM_NAME "Windows" CACHE INTERNAL "" FORCE)
  set(CMAKE_SYSTEM_VERSION "10.0" CACHE INTERNAL "" FORCE)
  set(CMAKE_SYSTEM_PROCESSOR "AMD64" CACHE INTERNAL "" FORCE)
  set(CMAKE_COMPILER_TARGET "x86_64-w64-windows-gnu" CACHE INTERNAL "")
else()
  message(FATAL_ERROR "Unsupported triplet: ${VCPKG_TARGET_TRIPLET}")
endif()

if(CMAKE_HOST_SYSTEM_NAME STREQUAL CMAKE_SYSTEM_NAME)
  set(CMAKE_CROSSCOMPILING OFF CACHE INTERNAL "" FORCE)
else()
  set(CMAKE_CROSSCOMPILING ON CACHE INTERNAL "" FORCE)
endif()

set(CMAKE_C_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_CXX_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_NASM_COMPILER_TARGET "${CMAKE_COMPILER_TARGET}" CACHE INTERNAL "" FORCE)
set(CMAKE_SYSROOT "${ACE}/sys/${VCPKG_TARGET_TRIPLET}" CACHE INTERNAL "" FORCE)

# Runtime Path
if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  set(CMAKE_BUILD_RPATH_USE_ORIGIN ON CACHE BOOL "")
  set(CMAKE_BUILD_RPATH "${CMAKE_SYSROOT}/usr/lib/shared" CACHE PATH "")
endif()

# Search Paths
list(APPEND CMAKE_MODULE_PATH "${ACE}/res/cmake")
set(CMAKE_SYSTEM_PROGRAM_PATH "${ACE}/bin;${ACE}/share/powershell" CACHE PATH "")

set(CMAKE_FIND_ROOT_PATH "${CMAKE_SYSROOT};${ACE}/vcpkg/installed/${VCPKG_TARGET_TRIPLET}" CACHE INTERNAL "")
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY CACHE INTERNAL "" FORCE)
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM BOTH CACHE INTERNAL "" FORCE)

if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  set(ENV{PKG_CONFIG_SYSROOT_DIR} "${CMAKE_SYSROOT}")
  set(ENV{PKG_CONFIG_PATH} "${CMAKE_SYSROOT}/usr/lib/x86_64-linux-gnu/pkgconfig")
  include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/lib/x86_64-linux-gnu/glib-2.0/include")
  include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/include/glib-2.0")
  include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/lib/x86_64-linux-gnu/dbus-1.0/include")
  include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/include/dbus-1.0")
  include_directories(AFTER SYSTEM "${CMAKE_SYSROOT}/usr/include/ibus-1.0")
endif()

# Generator
if(CMAKE_GENERATOR MATCHES "[Nn]inja")
  set(CMAKE_MAKE_PROGRAM "${ACE}/bin/ninja" CACHE INTERNAL "")
endif()

# Compiler
set(CMAKE_C_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_CXX_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)
set(CMAKE_ASM_NASM_COMPILER_WORKS ON CACHE INTERNAL "" FORCE)

set(CMAKE_C_COMPILER "${ACE}/bin/clang" CACHE FILEPATH "")
set(CMAKE_CXX_COMPILER "${ACE}/bin/clang++" CACHE FILEPATH "")
set(CMAKE_CXX_COMPILER_CLANG_SCAN_DEPS "${ACE}/bin/clang-scan-deps" CACHE FILEPATH "")
set(CMAKE_ASM_COMPILER "${ACE}/bin/clang" CACHE FILEPATH "")
set(CMAKE_ASM_NASM_COMPILER "${ACE}/bin/yasm" CACHE FILEPATH "")

if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  set(CMAKE_LINKER "${ACE}/bin/ld.lld" CACHE FILEPATH "")
elseif(CMAKE_SYSTEM_NAME STREQUAL "Windows")
  set(CMAKE_LINKER "${ACE}/bin/lld-link" CACHE FILEPATH "")
endif()

set(CMAKE_AR "${ACE}/bin/llvm-ar" CACHE FILEPATH "")
set(CMAKE_NM "${ACE}/bin/llvm-nm" CACHE FILEPATH "")
set(CMAKE_MT "${ACE}/bin/llvm-mt" CACHE FILEPATH "")
set(CMAKE_RANLIB "${ACE}/bin/llvm-ranlib" CACHE FILEPATH "")
set(CMAKE_DLLTOOL "${ACE}/bin/llvm-dlltool" CACHE FILEPATH "")
set(CMAKE_OBJCOPY "${ACE}/bin/llvm-objcopy" CACHE FILEPATH "")
set(CMAKE_OBJDUMP "${ACE}/bin/llvm-objdump" CACHE FILEPATH "")
set(CMAKE_STRIP "${ACE}/bin/llvm-strip" CACHE FILEPATH "")
set(CMAKE_SIZE "${ACE}/bin/llvm-size" CACHE FILEPATH "")

if(ACE_ENABLE_CLANG_TIDY)
  set(CMAKE_CXX_CLANG_TIDY "${ACE}/bin/clang-tidy" CACHE FILEPATH "")
endif()

set(CMAKE_RC_COMPILER "${ACE}/bin/llvm-windres" CACHE FILEPATH "")
set(CMAKE_RC_FLAGS_INIT "-I ${CMAKE_SYSROOT}/include")

# Compiler Flags
set(CMAKE_C_EXTENSIONS OFF CACHE BOOL "")
set(CMAKE_CXX_EXTENSIONS OFF CACHE BOOL "")

set(CMAKE_C_FLAGS_INIT "-march=x86-64-v3 -mavx2")
set(CMAKE_C_FLAGS_DEBUG_INIT "-Og -g")
set(CMAKE_C_FLAGS_RELEASE_INIT "-O3 -DNDEBUG -fomit-frame-pointer")
set(CMAKE_C_FLAGS_MINSIZEREL_INIT "-Oz -DNDEBUG -fomit-frame-pointer")
set(CMAKE_C_FLAGS_RELWITHDEBINFO_INIT "-O2 -g -DNDEBUG")
set(CMAKE_C_FLAGS_COVERAGE_INIT "-O0 -g -fno-omit-frame-pointer -fprofile-instr-generate -fcoverage-mapping")
set(CMAKE_C_FLAGS_PROFILE_INIT "-O3 -g -DNDEBUG -fno-omit-frame-pointer -fprofile-instr-generate")

if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  set(CMAKE_C_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} -fPIC")
endif()

if(CMAKE_SYSTEM_NAME STREQUAL "Windows")
  # Visual Studio 2022 Version 17.14 sets _MSC_VER to 1944.
  # https://learn.microsoft.com/cpp/overview/compiler-versions?view=msvc-170
  # MinGW is already configured to define WINVER=0x0A00 and _WIN32_WINNT=0x0A00.
  set(CMAKE_C_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} -fms-compatibility-version=19.44")
endif()

if(NOT ACE_DISABLE_IPO)
  set(CMAKE_C_FLAGS_RELEASE_INIT "${CMAKE_C_FLAGS_RELEASE_INIT} -flto")
endif()

if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(CMAKE_C_FLAGS_DEBUG_INIT "-O3 -DNDEBUG")
endif()

set(CMAKE_CXX_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} -fstrict-vtable-pointers")
set(CMAKE_CXX_FLAGS_DEBUG_INIT "${CMAKE_C_FLAGS_DEBUG_INIT}")
set(CMAKE_CXX_FLAGS_RELEASE_INIT "${CMAKE_C_FLAGS_RELEASE_INIT}")
set(CMAKE_CXX_FLAGS_MINSIZEREL_INIT "${CMAKE_C_FLAGS_MINSIZEREL_INIT}")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO_INIT "${CMAKE_C_FLAGS_RELWITHDEBINFO_INIT}")
set(CMAKE_CXX_FLAGS_COVERAGE_INIT "${CMAKE_C_FLAGS_COVERAGE_INIT}")
set(CMAKE_CXX_FLAGS_PROFILE_INIT "${CMAKE_C_FLAGS_PROFILE_INIT}")

if(NOT ACE_DISABLE_IPO)
  set(CMAKE_CXX_FLAGS_RELEASE_INIT "${CMAKE_CXX_FLAGS_RELEASE_INIT} -fwhole-program-vtables")
endif()

if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(CMAKE_C_FLAGS_INIT "${CMAKE_C_FLAGS_INIT} ${VCPKG_C_FLAGS}")
  set(CMAKE_C_FLAGS_DEBUG_INIT "${CMAKE_C_FLAGS_DEBUG_INIT} ${VCPKG_C_FLAGS_DEBUG}")
  set(CMAKE_C_FLAGS_RELEASE_INIT "${CMAKE_C_FLAGS_RELEASE_INIT} ${VCPKG_C_FLAGS_RELEASE}")

  set(CMAKE_CXX_FLAGS_INIT "${CMAKE_CXX_FLAGS_INIT} ${VCPKG_CXX_FLAGS}")
  set(CMAKE_CXX_FLAGS_DEBUG_INIT "${CMAKE_CXX_FLAGS_DEBUG_INIT} ${VCPKG_CXX_FLAGS_DEBUG}")
  set(CMAKE_CXX_FLAGS_RELEASE_INIT "${CMAKE_CXX_FLAGS_RELEASE_INIT} ${VCPKG_CXX_FLAGS_RELEASE}")
endif()

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_MINSIZEREL "${CMAKE_C_FLAGS_MINSIZEREL_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_RELWITHDEBINFO "${CMAKE_C_FLAGS_RELWITHDEBINFO_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_COVERAGE "${CMAKE_C_FLAGS_COVERAGE_INIT}" CACHE STRING "")
set(CMAKE_C_FLAGS_PROFILE "${CMAKE_C_FLAGS_PROFILE_INIT}" CACHE STRING "")

set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_MINSIZEREL "${CMAKE_CXX_FLAGS_MINSIZEREL_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_COVERAGE "${CMAKE_CXX_FLAGS_COVERAGE_INIT}" CACHE STRING "")
set(CMAKE_CXX_FLAGS_PROFILE "${CMAKE_CXX_FLAGS_PROFILE_INIT}" CACHE STRING "")

# Modules
if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(CMAKE_CXX_SCAN_FOR_MODULES OFF CACHE BOOL "")
elseif(CMAKE_CXX_MODULE_STD)
  list(APPEND CMAKE_PROJECT_INCLUDE ${CMAKE_CURRENT_LIST_DIR}/modules.cmake)
  set(CMAKE_EXPERIMENTAL_CXX_IMPORT_STD "d0edc3af-4c50-42ea-a356-e2862fe7a444" CACHE STRING "")
  set(CMAKE_CXX_COMPILER_IMPORT_STD "23;26" CACHE STRING "" FORCE)
endif()

# Optimizations
cmake_policy(SET CMP0069 NEW)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_DEBUG OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELEASE OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_MINSIZEREL OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELWITHDEBINFO OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_COVERAGE OFF CACHE BOOL "")
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_PROFILE OFF CACHE BOOL "")

# Code Generation
if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  cmake_policy(SET CMP0083 NEW)
  set(CMAKE_POSITION_INDEPENDENT_CODE OFF CACHE BOOL "")
endif()

# Runtime Library
if(CMAKE_HOST_SYSTEM_NAME STREQUAL "Windows")
  cmake_policy(SET CMP0091 NEW)
  set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:DLL>" CACHE INTERNAL "" FORCE)
endif()

# Debug Information
if(CMAKE_HOST_SYSTEM_NAME STREQUAL "Windows")
  cmake_policy(SET CMP0141 NEW)
  set(CMAKE_MSVC_DEBUG_INFORMATION_FORMAT "" CACHE INTERNAL "" FORCE)
endif()

# Linker Flags
cmake_policy(SET CMP0056 NEW)
foreach(LINKER SHARED MODULE EXE)
  if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(CMAKE_${LINKER}_LINKER_FLAGS_INIT "-pthread")
    if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
      set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT "-s")
    else()
      set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT "")
    endif()
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT "-s")
    set(CMAKE_${LINKER}_LINKER_FLAGS_MINSIZEREL_INIT "-s")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT "")
    set(CMAKE_${LINKER}_LINKER_FLAGS_COVERAGE_INIT "")
    set(CMAKE_${LINKER}_LINKER_FLAGS_PROFILE_INIT "")
  elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL "Windows")
    set(CMAKE_${LINKER}_LINKER_FLAGS_INIT "-Xlinker /MANIFEST:NO")
    if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
      set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT "-s -Xlinker /OPT:REF -Xlinker /OPT:ICF -Xlinker /INCREMENTAL")
    else()
      set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL")
    endif()
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT "-s -Xlinker /OPT:REF -Xlinker /OPT:ICF -Xlinker /INCREMENTAL:NO")
    set(CMAKE_${LINKER}_LINKER_FLAGS_MINSIZEREL_INIT "-s -Xlinker /OPT:REF -Xlinker /OPT:ICF -Xlinker /INCREMENTAL:NO")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL")
    set(CMAKE_${LINKER}_LINKER_FLAGS_COVERAGE_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL")
    set(CMAKE_${LINKER}_LINKER_FLAGS_PROFILE_INIT "-Xlinker /DEBUG -Xlinker /INCREMENTAL:NO")
  endif()

  if(DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
    set(CMAKE_${LINKER}_LINKER_FLAGS_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_INIT} ${VCPKG_LINKER_FLAGS}")
    set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT} ${VCPKG_LINKER_FLAGS_DEBUG}")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_RELEASE_INIT} ${VCPKG_LINKER_FLAGS_RELEASE}")
  endif()

  if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT
    "${CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT} -L${CMAKE_SYSROOT}/usr/lib/shared")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT
    "${CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT} -L${CMAKE_SYSROOT}/usr/lib/shared")
  elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL "Windows")
    set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT
    "${CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT} -L${CMAKE_SYSROOT}/lib/shared")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT
    "${CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT} -L${CMAKE_SYSROOT}/lib/shared")
  endif()

  if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_DEBUG_INIT} -Wl,--rpath=${CMAKE_SYSROOT}/usr/lib/shared")
    set(CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_RELWITHDEBINFO_INIT} -Wl,--rpath=${CMAKE_SYSROOT}/usr/lib/shared")
    set(CMAKE_${LINKER}_LINKER_FLAGS_COVERAGE_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_COVERAGE_INIT} -Wl,--rpath=${CMAKE_SYSROOT}/usr/lib/shared")
    set(CMAKE_${LINKER}_LINKER_FLAGS_PROFILE_INIT
     "${CMAKE_${LINKER}_LINKER_FLAGS_PROFILE_INIT} -Wl,--rpath=${CMAKE_SYSROOT}/usr/lib/shared")
  endif()
endforeach()

# Configurations
set_property(GLOBAL PROPERTY DEBUG_CONFIGURATIONS Debug RelWithDebInfo Coverage Profile)
set(CMAKE_MAP_IMPORTED_CONFIG_MINSIZEREL ";Release" CACHE INTERNAL "" FORCE)
set(CMAKE_MAP_IMPORTED_CONFIG_RELWITHDEBINFO ";Debug" CACHE INTERNAL "" FORCE)
set(CMAKE_MAP_IMPORTED_CONFIG_COVERAGE ";Debug" CACHE INTERNAL "" FORCE)
set(CMAKE_MAP_IMPORTED_CONFIG_PROFILE ";Debug" CACHE INTERNAL "" FORCE)

# Platform Variables
set(CMAKE_TRY_COMPILE_PLATFORM_VARIABLES
  BUILD_SHARED_LIBS
  CMAKE_BUILD_RPATH
  CMAKE_TOOLCHAIN_FILE
  CACHE STRING "")

# Tests
set_property(GLOBAL PROPERTY CTEST_TARGETS_ADDED 1)

# Vcpkg
if(NOT DEFINED VCPKG_CHAINLOAD_TOOLCHAIN_FILE)
  set(ENV{VCPKG_DISABLE_METRICS} "1")
  set(ENV{VCPKG_FORCE_SYSTEM_BINARIES} "1")
  set(ENV{VCPKG_WORKS_SYSTEM_BINARIES} "1")
  set(VCPKG_ROOT "${ACE}/vcpkg" CACHE PATH "")
  set(VCPKG_INSTALL_OPTIONS "--x-no-default-features")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--no-print-usage")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--overlay-ports=${ACE}/res/ports")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--overlay-triplets=${ACE}/res/triplets")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--feature-flags=-binarycaching")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--host-triplet=linux")
  set(VCPKG_INSTALL_OPTIONS "${VCPKG_INSTALL_OPTIONS};--triplet=${VCPKG_TARGET_TRIPLET}")
  if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(VCPKG_KEEP_ENV_VARS "PKG_CONFIG_SYSROOT_DIR;PKG_CONFIG_PATH")
  endif()
  set(Z_VCPKG_POWERSHELL_PATH "${ACE}/share/powershell/pwsh")
  include("${VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake")
endif()
